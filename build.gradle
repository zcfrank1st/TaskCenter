group 'com.chaos'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        kotlinVersion = '1.1.2-4'
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}


subprojects {
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        compile group: 'org.jooq', name: 'jooq', version: '3.9.3'
        compile group: 'org.jooq', name: 'jooq-meta', version: '3.9.3'

        compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '1.5.9'
        compile group: 'com.zaxxer', name: 'HikariCP', version: '2.6.2'

        compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        compile group: 'joda-time', name: 'joda-time', version: '2.9.9'

        compile group: 'com.typesafe', name: 'config', version: '1.3.1'
    }
}

project(':admin') {
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':jooq-module')
        compile('org.springframework.boot:spring-boot-starter-web')
    }
}

project(':executor') {
    apply plugin:  'application'

    dependencies {
        compile project(':jooq-module')
        compile 'com.github.salomonbrys.kodein:kodein:4.0.0'
        compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '0.16'


        compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.8.0'
    }
}

project(':dispatcher') {
    apply plugin:  'application'

    dependencies {
        compile project(':jooq-module')
        compile group: 'com.cronutils', name: 'cron-utils', version: '6.0.1'
        compile 'com.github.salomonbrys.kodein:kodein:4.0.0'
        compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '0.16'
    }
}

project(':jooq-module') {
    dependencies {
        compile group: 'org.jooq', name: 'jooq-codegen', version: '3.9.3'
    }
}